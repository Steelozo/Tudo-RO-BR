//===== rAthena Script ======================================= 
//= O script do sistema do Aeroplano
//===== By: ================================================== 
//= Equipe de desenvolvimento rAthena
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: ===================================== 
//= Projeto rAthena
//===== Description: ========================================= 
//= O Sistema de Aeroplano usado nos servidores oficiais.
//===== Additional Comments: ================================= 
//= 1.0 Primeira versão.
//============================================================ 
//============================================================ 
//= Traduzido por Steelozo
//============================================================ 
//============================================================ 
//= Rota do Aeroplano Doméstico
//= ... -> Juno -> Einbroch -> Lighthalzen -> Einbroch
//= -> Juno -> Hugel -> ...
//============================================================ 

airplane,243,73,0	script	#AirshipWarp-1	45,1,1,{
	end;

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc();
	end;

OnUnhide:
	enablenpc();
	specialeffect EF_SUMMONSLAVE;
	end;

OnTouch_:
	switch ($@airplanelocation) {
	case 0: warp "yuno",92,260; end;
	case 1: warp "einbroch",92,278; end;
	case 2: warp "lighthalzen",302,75; end;
	case 3: warp "hugel",181,146; end;
	}
}

airplane,243,29,0	duplicate(#AirshipWarp-1)	#AirshipWarp-2	45,1,1

airplane,1,1,0	script	Domestic_Airship	-1,{
	end;
OnInit:
	initnpctimer;
	end;
OnTimer20000:
	mapannounce "airplane","Estamos a caminho de Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer50000:
	mapannounce "airplane","Em breve chegaremos a Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer60000:
	set $@airplanelocation,1;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bem-vindo a Einbroch. Faça uma boa viagem.",bc_map,"0x00ff00";
	end;
OnTimer70000:
	mapannounce "airplane","Atualmente estamos em Einbroch. O Aeroplano decolará em breve.",bc_map,"0x00ff00";
	end;
OnTimer80000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","O Aeroplano está decolando agora. Nosso próximo destino é Lighthalzen.",bc_map,"0x70dbdb";
	end;
OnTimer100000:
	mapannounce "airplane","Estamos a caminho de Lighthalzen.",bc_map,"0x70dbdb";
	end;
OnTimer130000:
	mapannounce "airplane","Em breve chegaremos a Lighthalzen.",bc_map,"0x70dbdb";
	end;
OnTimer140000:
	set $@airplanelocation,2;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bem-vindo ao Lighthalzen. Faça uma boa viagem.",bc_map,"0x70dbdb";
	end;
OnTimer150000:
	mapannounce "airplane","No momento estamos em Lighthalzen. O Aeroplano partirá em breve.",bc_map,"0x70dbdb";
	end;
OnTimer160000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","O Aeroplano está decolando agora. Nosso próximo destino é Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer180000:
	mapannounce "airplane","Estamos a caminho de Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer210000:
	mapannounce "airplane","Em breve chegaremos a Einbroch.",bc_map,"0x00FF00";
	end;
OnTimer220000:
	set $@airplanelocation,1;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bem-vindo a Einbroch. Faça uma boa viagem.",bc_map,"0x00ff00";
	end;
OnTimer230000:
	mapannounce "airplane","No momento estamos em Einbroch. O Aeroplano vai decolar em breve.",bc_map,"0x00ff00";
	end;
OnTimer240000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","O Aeroplano está decolando agora. Nosso próximo destino é Juno.",bc_map,"0xff8200";
	end;
OnTimer260000:
	mapannounce "airplane","Estamos a caminho de Juno.",bc_map,"0xff8200";
	end;
OnTimer290000:
	mapannounce "airplane","Em breve chegaremos a Juno.",bc_map,"0xff8200";
	end;
OnTimer300000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bem-vindo a Juno. Faça uma boa viagem.",bc_map,"0xff8200";
	end;
OnTimer310000:
	mapannounce "airplane","No momento estamos em Juno. O Aeroplano partirá em breve.",bc_map,"0xff8200";
	end;
OnTimer320000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","O Aeroplano está decolando agora. Nosso próximo destino é Hugel.",bc_map,"0xca4bf3";
	end;
OnTimer340000:
	mapannounce "airplane","Estamos a caminho de Hugel.",bc_map,"0xca4bf3";
	end;
OnTimer370000:
	mapannounce "airplane","Em breve chegaremos a Hugel.",bc_map,"0xca4bf3";
	end;
OnTimer380000:
	set $@airplanelocation,3;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bem-vindo ao Hugel. Faça uma boa viagem.",bc_map,"0xca4bf3";
	end;
OnTimer390000:
	mapannounce "airplane","No momento estamos em Hugel. O Aeroplano partirá em breve.",bc_map,"0xca4bf3";
	end;
OnTimer400000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","O Aeroplano está decolando agora. Nosso próximo destino é Juno.",bc_map,"0xff8200";
	end;
OnTimer420000:
	mapannounce "airplane","Estamos a caminho de Juno.",bc_map,"0xff8200";
	end;
OnTimer450000:
	mapannounce "airplane","Em breve chegaremos a Juno.",bc_map,"0xff8200";
	end;
OnTimer460000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bem-vindo a Juno. Faça uma boa viagem.",bc_map,"0xff8200";
	end;
OnTimer470000:
	mapannounce "airplane","No momento estamos em Juno. O Aeroplano partirá em breve.",bc_map,"0xff8200";
	end;
OnTimer480000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","O Aeroplano está decolando agora. Nosso próximo destino é Einbroch.",bc_map,"0x00ff00";
	stopnpctimer;
	initnpctimer;
}

airplane,240,64,5	script	Saída#airplane1a::ExitAirplane	857,{
	end;
}
airplane,247,64,5	duplicate(ExitAirplane)	Saída#airplane1b	857
airplane,240,40,1	duplicate(ExitAirplane)	Saída#airplane2a	857
airplane,247,40,1	duplicate(ExitAirplane)	Saída#airplane2b	857

airplane,100,69,3	script	Tripulação do Aeroplano#ein-1	852,{
	mes "[Tripulação do Aeroplano]";
	mes "Se aterrissamos no seu destino e você gostaria de deixar o Aeroplano, por favor, use as escadas à frente.";
	mes "Obrigado por seu patrocínio.";
	close;
}

airplane,64,94,1	script	Criança de Umbala#ein_p	787,{
	emotion ET_PROFUSELY_SWEAT;
	mes "[Criança]";
	if (event_umbala >= 3) {
		mes "Uau, mãe!";
		mes "O-olha isso!";
		mes "Estamos voando! E-Estamos...";
		mes "Estamos no céu!";
	}
	else {
		mes "Makumalagu!";
		mes "Saampa joojimbo";
		mes "kaku na jedi Solo.";
		mes "Bwahahahahahahaah!";
	}
	close;
}

airplane,66,93,3	script	Senhora da Umbala#ein_p	783,{
	emotion ET_THINK;
	mes "[Senhora]";
	if (event_umbala >= 3) {
		mes "Calma...";
		mes "Querida, comporte-se~";
		mes "Não fique tão animado";
		mes "quando estivermos em um";
		mes "lugar público como este!";
	}
	else {
		mes "Chooktu!";
		mes "Sacraup matii!";
		mes "Shaka gurftalfi";
		mes "huntiki manjoo!";
	}
	close;
}

airplane,71,91,7	script	Homem de Umbala#ein_p	789,{
	if (event_umbala >= 3) {
		mes "[Chrmlim]";
		mes "Olá~";
		mes "A partir desse olhar";
		mes "em seu rosto, vejo que";
		mes "você pode me entender.";
		mes "^333333*Uau...!*^000000";
		next;
		emotion ET_HNG;
		mes "[Chrmlim]";
		mes "Eu tenho ajudado a empresa";
		mes "do Aeroplano fazendo os";
		mes "tripulantes do Aeroplano";
		mes "treinarem em Umbala para";
		mes "superar qualquer acrofobia que possam ter através do bungee jumping. Legal, hein?";
		next;
		mes "[Chrmlim]";
		mes "Mas...";
		mes "Alguns deles não conseguiam";
		mes "superar o medo de altura.";
		mes "E alguns até acabaram,";
		mes "hum, ^333333em Niflheim^000000.";
	}
	else {
		mes "[Chrmlim]";
		mes "Bajoo ga";
		mes "nukta Aeroplano.";
		next;
		mes "[Chrmlim]";
		mes "...";
		mes "......";
		next;
		emotion ET_HNG;
		mes "[Chrmlim]";
		mes "Shabala moow bajama";
		mes "Aeroplano kulaha googoona ";
		mes "salu. Dama, kookoo na nu";
		mes "yukuta. Um, fashuku na ret!";
	}
	close;
}

airplane,250,58,2	script	Equipe do Aeroplano#airplane	67,{
	// Hugel quest addition
	if (hg_ma1 == 3) {
		mes "[Equipe do Aeroplano]";
		mes "Bem-vindo ao Aeroplano.";
		mes "Como posso ajudá-lo?";
		next;
		select("Tens um passageiro chamado Thierry?");
		mes "[Equipe do Aeroplano]";
		mes "Lamento, mas acho que não temos um passageiro com esse nome.";
		close;
	}
	// Hugel quest end
	mes "[Equipe do Aeroplano]";
	mes "Bem-vindo ao Aeroplano.";
	mes "Como posso ajudá-lo?";
	next;
	switch (select("Usando o Aeroplano:Cabine do Capitão:Instalações:Cancelar")) {
	case 1:
		mes "[Equipe do Aeroplano]";
		mes "Quando você ouvir uma transmissão anunciando que chegamos ao seu destino, por favor, use uma das saídas localizadas nas extremidades norte e sul do Aeroplano.";
		next;
		mes "[Equipe do Aeroplano]";
		mes "Se você perder sua parada, não se preocupe. O Aeroplano está constantemente em rota e você terá outra chance de chegar ao seu destino pretendido.";
		close;
	case 2:
		mes "[Equipe do Aeroplano]";
		mes "A Cabine do Capitão está localizada na frente do Aeroplano. Lá, você pode conhecer o capitão e o piloto do Aeroplano.";
		close;
	case 3:
		mes "[Equipe do Aeroplano]";
		mes "O Dirigível disponibiliza diversos Mini Games para o entretenimento de todos os nossos passageiros. Convidamos você a tentar sua sorte e habilidades nos Minijogos do Aeroplano~";
		close;
	case 4:
		mes "[Equipe do Aeroplano]";
		mes "Bem, espero que seu vôo a bordo de nossos Aeroplano. Obrigado e tenha um bom dia.";
		close;
	}
}

airplane,80,71,2	script	Zerta#01airplane	834,{
	mes "[Zerta]";
	mes "Ah, olá aventureiro.";
	mes "Eu estou atualmente em uma jornada sagrada, oferecendo oração pelo bem do continente Midgard.";
	close;
}

airplane,65,63,4	script	Maelin#01airplane	714,{
	mes "[Maelin]";
	mes "Hum, este dirigível é para Lutie, não é?";
	mes "Esperei tanto, mas não ouvi nenhuma transmissão sobre Lutie.";
	close;
}

airplane,72,34,6	script	Aanos#01airplane	702,{
	mes "[Aanos]";
	mes "Oh uaaau~";
	mes "O céu parece tão diferente e bonito lá de cima!";
	close;
}

airplane,221,158,2	script	Piloto#airplane	852,{
	// Hugel quest addition
	if (hg_ma1 == 3) {
		mes "[Piloto]";
		mes "Eu gostaria de poder ir beber uma cerveja fresca e gelada.";
		mes "Beber é o objetivo da minha vida! Beber me dá energia!";
		mes "Não sou nada sem bebidas!";
		next;
		mes "[Piloto]";
		mes "Mas! Dirigir sob a influência não é bom.";
		mes "Mas! Isso me faz querer beber cada vez mais!";
		emotion ET_CRY;
		next;
		select("Conhece algum passageiro chamado Thierry?");
		mes "[Piloto]";
		mes "Este uniforme é muito elegante, mas é muito grosso para usar no Aeroplano.";
		next;
		mes "[Piloto]";
		mes "...";
		mes "......";
		mes "Ninguém realmente entra nesta sala.";
		mes "E o capitão É uma rena. Eu poderia simplesmente ficar de cueca.";
		next;
		emotion ET_HUK;
		mes "[Piloto]";
		mes "Ué!? Quem é esse!";
		next;
		mes "- ...Ele não está te ouvindo, de forma alguma. -";
		close;
	}
	// Hugel quest end
	switch (rand(1,4)) {
	case 1:
		mes "[Piloto]";
		mes "Faz tanto tempo que eu não aprecio uma boa e gelada cerveja alcoólica. Mas o trabalho exige que eu seja o mais claro possível!";
		next;
		mes "[Piloto]";
		mes "Beba sempre com responsabilidade!";
		mes "Ainda assim, não me lembro da última vez que tirei férias de verdade ou até mesmo um dia de folga. Sim, um pouco de bebida, algumas batatas fritas, um pouco de TV e sério R&R nesta ordem.";
		emotion ET_CRY;
		close;
	case 2:
		mes "[Piloto]";
		mes "Cara, o tempo está muito bom hoje. Céus claros e abertos, proporcionam boa visibilidade e voos seguros e despreocupado.";
		close;
	case 3:
		mes "[Piloto]";
		mes "Sabe, nosso capitão é um cara respeitável. Ele e seu irmão são bem conhecidos na indústria aeronáutica.";
		mes "Quem sabia que as renas davam bons capitães?";
		next;
		mes "[Piloto]";
		mes "Só entre você e eu, eu tenho que te dizer, que o Papai Noel estava em alguma coisa, fazendo com que renas e elfos trabalhassem para ele.";
		mes "O homem deve ser um gênio!";
		close;
	default:
		mes "[Piloto]";
		mes "Você sabe, toda essa coisa de pilotar no ar, é bem novo, sabe? Sim, eles conseguiram esta operação de Aeroplano às pressas.";
		next;
		emotion ET_HUK;
		mes "[Piloto]";
		mes "Ainda assim, eles eram muito sérios, realmente pensavam no futuro. Quero dizer, eles nos treinaram enquanto os dirigíveis ainda estavam sendo inventados.";
		mes "Isso não é louco?!";
		close;
	}
}

airplane,50,66,5	script	Comerciante de Maçã#airplane	86,{
	mes "[Fruitz]";
	mes "Bem-vindo à loja da Fruitz, onde você pode comprar maçãs ou moê-las para fazer suco de Maçã.";
	next;
	switch (select("Comprar Maçãs.:Fazer suco de maçã.:Por quê você está aqui?:Cancelar.")) {
	case 1:
		mes "[Fruitz]";
		mes "Por favor, insira a quantidade de Maçãs que você deseja comprar. Cada Maçã custa 15 zeny e você pode comprar no máximo 500 por vez. Por favor, insira '0' para cancelar seu pedido.";
		next;
		while (1) {
			input .@input;
			set .@pay, .@input * 15;
			if (.@input == 0) {
				mes "[Fruitz]";
				mes "Obrigado por parar por minha loja. Até a próxima!";
				mes "Venha a qualquer momento quando você sentir vontade de comer uma Maçã~";
				close;
			}
			else if (.@input < 1 || .@input > 500) {
				mes "[Fruitz]";
				mes "Você digitou um número maior que o valor máximo de 500.";
				mes "Por favor, digite o número de Maçãs que deseja comprar novamente.";
				next;
			}
			else {
				mes "[Fruitz]";
				mes "Um total de ^FF0000" + .@input + "^000000 Maçãs";
				mes "vai te custar ^FF0000" + .@pay + "^000000 zeny.";
				mes "Deseja continuar?";
				next;
				if (select("Yes:No") == 2) {
					mes "[Fruitz]";
					mes "Obrigado por visitar minha loja. Até a próxima!";
					mes "Venha a qualquer momento quando você sentir vontade de comer uma Maçã~";
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes "[Fruitz]";
			mes "Sinto muito, mas você não tem dinheiro suficiente para comprar tantas Maçãs.";
			mes "Verifique seu zeny ou compre menos Maçãs.";
			close;
		}
		else if (checkweight(512,.@input) == 0) {
			mes "[Fruitz]";
			mes "Hmmm, acho que você não tem espaço suficiente em seu inventário para carregar tantas maçãs. Por que você não libera um pouco do seu espaço de inventário?";
			close;
		}
		else {
			set Zeny, Zeny - .@pay;
			getitem 512,.@input; //Apple
			mes "[Fruitz]";
			mes "Obrigado por visitar minha loja. Espero que gostem do sabor dessas Maçãs~!";
			close;
		}
	case 2:
		mes "[Fruitz]";
		mes "Ok, vou precisar de";
		mes "^FF00003 Maçãs e 1 Garrafa Vazia^000000 para fazer 1 suco de maçã para você.";
		mes "Deseja continuar?";
		next;
		switch (select("Sim:Não")) {
		case 1:
			if (countitem(512) < 3 || countitem(713) < 1) {
				mes "[Fruitz]";
				mes "Desculpe, mas você não tem materiais suficientes para criar uma garrafa de suco de maçã. Lembre-se, eu preciso de ^FF00003 Maçãs e 1 Garrafa Vazia^000000 para fazer isso.";
				close;
			}
			else {
				mes "[Fruitz]";
				mes "Obrigado, por favor, aguarde um momento.";
				next;
				mes "^3355FF*Moer moer*";
				mes "*Moer moer*";
				mes "*Clang...!*^000000";
				next;
				delitem 512,3; //Maçã
				delitem 713,1; //Garrafa vazia
				getitem 531,1; //Suco de Maçã
				mes "[Fruitz]";
				mes "Pronto~";
				mes "Espero que goste!";
				mes "Por favor, sinta-se à vontade para parar para suas necessidades de Maçã e Suco de Maçã a qualquer momento, aventureiro~";
				close;
			}
		case 2:
			mes "[Fruitz]";
			mes "Obrigado por visitar minha loja. Até a próxima! Venha a qualquer momento quando você sentir vontade de comer uma Maçã ~";
			close;
		}
	case 3:
		mes "[Fruitz]";
		mes "Eu costumava ser um vagabundo errante quando, um dia, tirei uma soneca e algo bateu na minha cabeça e me acordou do meu sono repousante.";
		next;
		mes "[Fruitz]";
		mes "Acontece que eu estava dormindo sob uma macieira e uma Maçã caiu e me atingiu na cabeça. Eu estava morrendo de fome e estava prestes a comer aquela Maçã...";
		next;
		mes "[Fruitz]";
		mes "Mas de repente, Kain, meu velho amigo dos dias de mineração, me pediu para ajudá-lo no Aeroplano. Então eu fiz, e foi lá que eu encontrei algumas pessoas jogando o jogo de Dados.";
		next;
		mes "[Fruitz]";
		mes "Eu estava entediado e curioso e acabei apostando aquele único Maçã em um jogo de Dados. Mas, por alguma razão, tive essa incrível sequência de sorte. Uma Maçã se tornou duas...";
		next;
		mes "[Fruitz]";
		mes "Dois se tornaram quatro e antes que eu percebesse, eu tinha conquistado o mercado da Maçã! Ganhei tantas Maçãs que acabei de começar meu próprio negócio aqui no Aeroplano. Estranho, hein?";
		next;
		mes "[Fruitz]";
		mes "Então as Maçãs são boas para você. Eles certamente foram muito bons para mim. Hahahaha~!";
		close;
	case 4:
		mes "[Fruitz]";
		mes "Obrigado por utilizar minha loja.";
		mes "Adeus~";
		close;
	}
}

//============================================================ 
//= Rota Internacional do Aeroplano
//= ... -> Izlude -> Juno -> Rachel -> ...
//============================================================ 

airplane_01,243,73,0	script	#AirshipWarp-3	45,1,1,{
	end;
	
OnTouch_:
	switch ($@airplanelocation2) {
	case 0: warp "ra_fild12",292,204; end;
	case 1: 
		if(checkre(0))
			warp "izlude",200,73;
		else
			warp "izlude",200,56;
		end;
	case 2: warp "yuno",12,261; end;
	}

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc();
	end;
	
OnUnhide:
	enablenpc();
	specialeffect EF_SUMMONSLAVE;
	end;
}

airplane_01,243,29,0	duplicate(#AirshipWarp-3)	#AirshipWarp-4	45,1,1

airplane_01,1,1,0	script	International_Airship	-1,{
	end;
OnInit:
OnEnable:
	initnpctimer;
	end;
OnTimer25000:
	mapannounce "airplane_01","Estamos a caminho de Izlude.",bc_map,"0x00ff00";
	end;
OnTimer50000:
	mapannounce "airplane_01","Em breve chegaremos a Izlude.",bc_map,"0x00ff00";
	end;
OnTimer60000:
	set $@airplanelocation2,1;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","Bem-vindo à Izlude. Faça uma boa viagem.",bc_map,"0x00ff00";
	end;
OnTimer70000:
	mapannounce "airplane_01","Atualmente, estamos em Izlude. O Aeroplano decolará em breve.",bc_map,"0x00ff00";
	end;
OnTimer80000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","O Aeroplano está decolando agora. Nosso próximo destino é Juno.",bc_map,"0x70dbdb";
	end;
OnTimer105000:
	mapannounce "airplane_01","Estamos a caminho de Juno.",bc_map,"0x70dbdb";
	end;
OnTimer130000:
	mapannounce "airplane_01","Em breve chegaremos a Juno.",bc_map,"0x70dbdb";
	end;
OnTimer140000:
	set $@airplanelocation2,2;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","Bem-vindo a Juno. Faça uma boa viagem.",bc_map,"0x70dbdb";
	end;
OnTimer150000:
	mapannounce "airplane_01","Atualmente estamos em Juno. O Aeroplano decolará em breve.",bc_map,"0x70dbdb";
	end;
OnTimer160000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","O Aeroplano está decolando agora. Nosso próximo destino é Rachel.",bc_map,"0xFF8200";
	end;
OnTimer185000:
	mapannounce "airplane_01","Estamos a caminho de Rachel.",bc_map,"0xFF8200";
	end;
OnTimer210000:
	mapannounce "airplane_01","Em breve chegaremos a Rachel.",bc_map,"0xFF8200";
	end;
OnTimer220000:
	set $@airplanelocation2,0;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","Bem-vindo a Raquel. Faça uma boa viagem.",bc_map,"0xFF8200";
	end;
OnTimer230000:
	mapannounce "airplane_01","Atualmente estamos em Rachel. O Aeroplano decolará em brev.",bc_map,"0xFF8200";
	end;
OnTimer240000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","O Aeroplano está decolando agora. Nosso próximo destino é Izlude.",bc_map,"0x00ff00";
	stopnpctimer;
	set .moninv, .moninv + 1;
	if (.moninv == 7) {
		if (rand(1,3) == 3) {
			donpcevent "Airship#airplane02::OnEnable";
			end;
		}
		set .moninv, 0;
	}
	initnpctimer;
	end;
}

airplane_01,240,64,5	script	Saída#airplane_011a::ExitAirplane01	857,{
	end;
}
airplane_01,247,64,5	duplicate(ExitAirplane01)	Saída#airplane_011b	857
airplane_01,240,40,1	duplicate(ExitAirplane01)	Saída#airplane_012a	857
airplane_01,247,40,1	duplicate(ExitAirplane01)	Saída#airplane_012b	857

airplane_01,250,58,2	script	Equipe do Aeroplano#airplane01	67,{
	mes "[Equipe do Aeroplano]";
	mes "Bem-vindo ao Aeroplano.";
	mes "Como posso ajudá-lo?";
	next;
	switch (select("Usar o Aeroplano:Cabine do Capitão:Instalações:Cancelar")) {
	case 1:
		mes "[Equipe do Aeroplano]";
		mes "Quando você ouvir uma transmissão anunciando que chegamos ao seu destino, use uma das saídas localizadas nas extremidades norte e sul do Aeroplano.";
		next;
		mes "[Equipe do Aeroplano]";
		mes "Se acontecer de você perder a sua parada, não se preocupe. O Aeroplano está constantemente em rota e você terá outra chance de chegar ao seu destino pretendido.";
		close;
	case 2:
		mes "[Equipe do Aeroplano]";
		mes "A Cabine do Capitão está localizada na frente do Aeroplano. Lá, você pode conhecer o capitão e o piloto do Aeroplano.";
		close;
	case 3:
		mes "[Equipe do Aeroplano]";
		mes "O Aeroplano disponibiliza diversos Mini-Jogos para o entretenimento de todos os nossos passageiros. Convidamos você a tentar sua sorte e habilidades nos Minijogos do Aeroplano~";
		close;
	case 4:
		mes "[Equipe do Aeroplano]";
		mes "Bem, espero que seu vôo a bordo de nossos Aeroplano. Obrigado e tenha um bom dia.";
		close;
	}
}

airplane_01,50,66,5	script	Comerciante de Maçã#air01	86,{
	if (checkweight(1201,1) == 0) {
		mes "- Espere um minuto !! -";
		mes "- No momento, está carregando -";
		mes "- muitos itens com você. -";
		mes "- Por favor, tente novamente -";
		mes "- depois de perder algum peso. -";
		close;
	}
	mes "[Meltz]";
	mes "Bem-vindo à Loja do Meltz, onde você pode comprar maçãs ou moê-las para fazer suco de maçã.";
	next;
	switch (select("Comprar Maçãs.:Fazer Suco de Maçã.:Cancelar.")) {
	case 1:
		mes "[Meltz]";
		mes "Por favor, digite a quantidade de maçãs que você deseja comprar. Cada Maçã custa 15 zeny e você pode comprar no máximo 500 de cada vez. Por favor, digite '0' para cancelar seu pedido.";
		next;
		while (1) {
			input .@input;
			set .@pay, .@input * 15;
			if (.@input == 0) {
				mes "[Meltz]";
				mes "Obrigado por visitar minha loja. Até a próxima! Venha a qualquer momento quando você sentir vontade de comer uma Maçã~";
				close;
			}
			else if (.@input < 1 || .@input > 500) {
				mes "[Meltz]";
				mes "Você digitou um número maior que o valor máximo de 500. Por favor, digite o número de Maçãs que deseja comprar novamente.";
				next;
			}
			else {
				mes "[Meltz]";
				mes "Um total de ^FF0000" + .@input + "^000000 Maçãs";
				mes "vai te custar ^FF0000" + .@pay + "^000000 zeny.";
				mes "Deseja continuar?";
				next;
				if (select("Sim:Não") == 2) {
					mes "[Meltz]";
					mes "Obrigado por visitar minha loja. Até a próxima! Venha a qualquer momento quando você sentir vontade de comer uma Maçã~";
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes "[Meltz]";
			mes "Sinto muito, você não tem";
			mes "dinheiro suficiente com você.";
			mes "Verifique seus fundos ou";
			mes "compre menos Maçãs.";
			close;
		}
		else if (checkweight(512,.@input) == 0) {
			mes "[Meltz]";
			mes "Hmm, acho que você não tem espaço suficiente para carregar tantas Maçãs. Talvez você queira liberar seu espaço de inventário.";
			close;
		}
		else {
			set Zeny, Zeny - .@pay;
			getitem 512,.@input; //Maçãs
			mes "[Meltz]";
			mes "Obrigado por visitar minha loja. Espero que gostem do sabor dessas Maçãs~!";
			close;
		}
	case 2:
		mes "[Meltz]";
		mes "Ok, vou precisar";
		mes "^FF00003 Maçãs e 1 Garrafa Vazia^000000 para fazer 1 Suco de Maçã para você.";
		mes "Deseja continuar?";
		next;
		switch (select("Sim:Não")) {
		case 1:
			if (countitem(512) < 3 || countitem(713) < 1) {
				mes "[Meltz]";
				mes "Sinto muito, mas você não tem materiais suficientes para criar uma garrafa de Suco de Maçã. Lembre-se, eu preciso de 3 Maçãs e 1 Garrafa Vazia para fazer isso.";
				close;
			}
			else {
				mes "[Meltz]";
				mes "Obrigado, por favor aguarde.";
				next;
				mes "^3355FF*Moer* *Moer*";
				mes "*Moer* *Moer*";
				mes "*Clang...!*^000000";
				next;
				delitem 512,3; //Maçã
				delitem 713,1; //Garrafa vazia
				getitem 531,1; //Suco de Maçã
				mes "[Meltz]";
				mes "Aí está~";
				mes "Por favor, volte sempre.";
				close;
			}
		case 2:
			mes "[Meltz]";
			mes "Obrigado por visitar minha loja. Até a próxima! Venha a qualquer momento quando você sentir vontade de comer uma Maçã~";
			close;
		}
	case 3:
		mes "[Meltz]";
		mes "Obrigado por visitar minha loja. Até a próxima! Venha a qualquer momento quando você sentir vontade de comer uma Maçã~";
		close;
	}
}

airplane_01,221,158,2	script	Piloto#airplane_01	852,{
	switch (rand(1,4)) {
	case 1:
		mes "[Piloto]";
		mes "Longitude, 131 graus leste.";
		mes "Latitude, 37 graus norte.";
		mes "Estamos no rumo certo, capitão.";
		close;
	case 2:
		mes "[Piloto]";
		mes "Parece um dia muito nublado. Sempre difícil de navegar quando o céu não está claro. Acho que precisaremos amplificar o radar.";
		close;
	case 3:
		mes "[Piloto]";
		mes "O Capitão é um bom homem e não consigo pensar em pessoa melhor para comandar. Ainda assim, ele é bem durão, um verdadeiro motorista de escravos.";
		next;
		mes "[^ff0000Tarlock^000000]";
		mes "^ff0000Ei...!^000000";
		mes "^ff0000Menos bate-papo^000000";
		mes "^ff0000e mais pilotagem!^000000";
		next;
		mes "[Piloto]";
		mes "I-imediatamente, senhor!";
		mes "(Vê o que eu quero dizer?)";
		close;
	default:
		mes "[Piloto]";
		mes "Este uniforme é muito elegante, mas é muito grosso para usar no Aeroplano.";
		next;
		mes "[Piloto]";
		mes "...";
		mes "......";
		mes "Ninguém realmente";
		mes "entra nesta sala.";
		mes "E o capitão É uma rena.";
		mes "Eu poderia simplesmente ficar de cueca.";
		next;
		emotion ET_HUK;
		mes "[Piloto]";
		mes "Oh...! Olá!";
		mes "A-a-aproveitando seu voo?!";
		close;
	}
}

airplane_01,83,61,2	script	Dianne#01airplane_01	72,2,2,{
	mes "[Dianne]";
	mes "Isto é tão estranho!";
	mes "Fui visitar o capitão do Aeroplano e tudo o que vi foi essa rena estranha. Oh!";
	mes "Você acha isso...";
	close;

OnTouch_:
	emotion ET_CRY;
	end;
}

airplane_01,69,63,2	script	Mendel#01airplane_01	55,{
	mes "[Mendel]";
	mes "Como eu esperava, as refeições a bordo são de qualidade três estrelas na melhor das hipóteses. *Harrrumph* Eu realmente deveria ter trazido meu chef para que eu pudesse desfrutar de uma verdadeira refeição.";
	close;
}

airplane_01,71,31,2	script	Espadachim Shimizu#air_01	106,{
	mes "[Espadachim Shimizu]";
	mes "Finalmente, depois de cinco anos de espera...";
	mes "Eu posso me vingar!";
	next;
	mes "[Espadachim Shimizu]";
	mes "Eu apenas...";
	mes "Tenho que me certificar de que não vou continuar perdendo minha parada. Mas em breve, muito em breve, a vingança será minha!";
	close;
}

//============================================================ 
//= Desafio de Digitação
//============================================================ 
airplane_01,32,61,4	script	Nils#ein	49,1,1,{
	mes "[Nils]";
	mes "Welcome to the";
	mes "^ff0000RO Typing Challenge^000000.";
	mes "Would you like to play";
	mes "a quick typing game?";
	next;
	switch (select("Play ^ff0000RO Typing Challenge^000000:Information:View Top Records:Cancel")) {
	case 1:
		mes "[Nils]";
		mes "Okay, we have";
		mes "a new challenger!";
		mes "Enter the following";
		mes "text as quickly as you";
		mes "can without making any";
		mes "mistakes! Let's start~!";
		setarray .@line1_1$[0],	"^3cbcbccallipygian salacius lascivious^000000",
					"^3cbcbcBy the power of^000000",
					"^0000ffthkelfkskeldmsiejdlslehfndkelsheidl^000000",
					"^3cbcbcburrdingdingdingdilidingdingdingphoohudaamb^000000",
					"^3cbcbcCoboman no chikara-yumei na^000000",
					"^3cbcbcI'm the king of All Weirdos! Now^000000",
					"^3cbcbcYou give me no choice. I guess it's^000000";
		setarray .@line1_2$[0],	"^3cbcbclicentious prurient concupiscent^000000",
					"^3cbcbcp-po-poi-po-poi-poin-poing^000000",
					"^3cbcbcskemd^000000",
					"^3cbcbcandoorabambarambambambambamburanbamding^000000",
					"^3cbcbcchikara-daiookii na chikara da ze!^000000",
					"^3cbcbcyou know of my true power. Obey~!^000000",
					"^3cbcbctime for me to reveal my secret...^000000";
		setarray .@line1_3$[0],	"",
					"^3cbcbcGOD-POING. I NEVER LOSE!^000000",
					"",
					"",
					"^3cbcbcCOBO ON^000000",
					"",
					"";
		setarray .@word1$[0],	"callipygian salacius lascivious licentious prurient concupiscent",
					"By the power of p-po-poi-po-poi-poin-poing GOD-POING. I NEVER LOSE!",
					"thkelfkskeldmsiejdlslehfndkelsheidlskemd",
					"burrdingdingdingdilidingdingdingphoohudaambandoorabambarambambambambamburanbamding",
					"Coboman no chikara-yumei na chikara-daiookii na chikara da ze! COBO ON",
					"I'm the king of All Weirdos! Now you know of my true power. Obey~!",
					"You give me no choice. I guess it's time for me to reveal my secret...";
		setarray .@line2_1$[0],	"^3cbcbcuNflAPPaBLe LoVaBLe SeCreTs AnD^000000",
					"^ff1493LiGhTsPeEd RiGhT SPEed LeFT TURn^000000",
					"^ff1493hfjdkeldjsieldjshfjdjeiskdlefvbd^000000",
					"^ff1493burapaphuralanderamduanbatuhiwooi^000000",
					"^ff1493belief love luck grimace sweat rush^000000",
					"^800080opeN, Open!op3n.openOpen0p3nOpEn0pen^000000",
					"^3cbcbcfReeDoM ecstAcy JoUrnaliSm ArMplt^000000";
		setarray .@line2_2$[0],	"^3cbcbcboWLIiNg aGaINST tHe KarMA of YoUtH^000000",
					"^ff1493RiGhT BuRn OrIGInAL GaNgSteR SmACk^000000",
					"",
					"^ff1493kabamturubamdingding^000000",
					"^ff1493folktale rodimus optimus bumblebee^000000",
					"^800080`open'0Pen open? open!111OPENSESAME^000000",
					"^3cbcbcDisCoverY hEaDaChE MoonbeAmS jUsTiCE^000000";
		setarray .@word2$[0],	"uNflAPPaBLe LoVaBLe SeCreTs AnD boWLIiNg aGaINST tHe KarMA of YoUtH",
					"LiGhTsPeEd RiGhT SPEed LeFT TURn RiGhT BuRn OrIGInAL GaNgSteR SmACk",
					"hfjdkeldjsieldjshfjdjeiskdlefvbd",
					"burapaphuralanderamduanbatuhiwooikabamturubamdingding",
					"belief love luck grimace sweat rush folktale rodimus optimus bumblebee",
					"opeN, Open!op3n.openOpen0p3nOpEn0pen`open'0Pen open? open!111OPENSESAME",
					"fReeDoM ecstAcy JoUrnaliSm ArMplt DisCoverY hEaDaChE MoonbeAmS jUsTiCE";
		setarray .@letters[0],	1300,
					1250,
					1180,
					1380,
					1740,
					1440,
					1450;
		set .@wordtest, rand(7);
		next;
		mes "[Nils]";
		mes .@line1_1$[.@wordtest];
		mes .@line1_2$[.@wordtest];
		mes .@line1_3$[.@wordtest];
		set .@start_time, gettimetick(1);
		next;
		input .@save1$;
		set .@end_time, gettimetick(1);
		set .@total_time, .@end_time - .@start_time;
		mes "[Nils]";
		mes .@line2_1$[.@wordtest];
		mes .@line2_2$[.@wordtest];
		set .@start_time, gettimetick(1);
		next;
		input .@save2$;
		set .@end_time, gettimetick(1);
		set .@total_time, .@total_time + (.@start_time - .@end_time);
		set .@tasoo, (.@letters[.@wordtest] / .@total_time) * 6;
		if ((.@save1$ == .@word1$[.@wordtest]) && (.@save2$ == .@word2$[.@wordtest])) {
			mes "[Nils]";
			mes "Seu registro é ^ff0000" + .@total_time + " segundos^000000 e";
			mes "as letras totais são" + .@tasoo + ".";
			next;
			if (.@tasoo >= 1300) {
				mes "[Nils]";
				mes "Hmmm, this record isn't";
				mes "humanly possible unless you";
				mes "copy and paste the whole";
				mes "sentence. Please play fairly";
				mes "next time.";
				close;
			}
			if (.@tasoo >= $050320_ein_typing) {
				mes "[Nils]";
				mes "The previous top record was";
				mes "made by ^0000ff" + $050320_minus1_typing$ + "^000000";
				mes "with the total ^0000ff" + $050320_ein_typing + "^000000 letters.";
				mes "However, ^ff0000" + strcharinfo(0) + "^000000,";
				mes "you made the new top record";
				mes "this time. Congratulations!";
				set $050320_minus1_typing$, strcharinfo(0);
				set $050320_ein_typing, .@tasoo;
				close;
			}
			else {
				mes "[Nils]";
				mes "^0000ff" + $050320_minus1_typing$ + "^000000";
				mes "is the current";
				mes "record holder with";
				mes "a letter total of ^0000ff" + $050320_ein_typing + "^000000";
				mes "characters. Try to beat";
				mes "that record next time~";
				close;
			}
		}
		else {
			mes "[Nils]";
			mes "Oooh...";
			mes "I'm sorry, but";
			mes "you entered the";
			mes "text incorrectly...";
			close;
		}
	case 2:
		mes "[Nils]";
		mes "The ^ff0000RO Typing Challenge^000000";
		mes "is a game where you enter";
		mes "the given text as quickly as you";
		mes "can. The name of the top player";
		mes "is recorded for posterity. If you";
		mes "want fame, here's your chance!";
		next;
		mes "[Nils]";
		mes "I'd just like to let";
		mes "you know that you type";
		mes "all the text that you see";
		mes "in the single input line that";
		mes "you're given. So don't press";
		mes "the enter key, just click 'OK.'";
		close;
	case 3:
		mes "[Nils]";
		mes "^0000ff" + $050320_minus1_typing$ + "^000000";
		mes "is the current";
		mes "record holder with";
		mes "a letter total of ^0000ff" + $050320_ein_typing + "^000000";
		mes "characters. Try to beat";
		mes "that record next time~";
		close;
	case 4:
		mes "[Nils]";
		mes "Sinta-se à vontade para assumir";
		mes "^ff0000Desafio de Digitação^000000";
		mes "qualquer hora. Eu estarei aqui~";
		close;
	}
}

//============================================================ 
//= Apple Gambling
//============================================================ 
airplane_01,33,68,4	script	Clarice	74,{
	mes "[Clarice]";
	mes "Oi, eu sou a Clarice~";
	mes "Como você gostaria de apostar algumas Maçãs em um amistoso jogo de dados?";
	next;
	callfunc "Jogo de Maçã","Clarice";
	end;
}

function	script	applegamble	{
	.@npc_name$ = getarg(0);
	switch (select("Jogar o Jogo de Dados::Aprenda as Regras do Jogo:Cancelar")) {
	case 3:
		mes "["+ .@npc_name$ +"]";
		mes "Estou pronto para um jogo de dados sempre que você quiser. Apenas fale comigo se você for atingido com o desejo repentino de jogar, ok?";
		close;
	case 2:
		mes "["+ .@npc_name$ +"]";
		mes "As regras do jogo de dados são bem simples. Primeiro, você faz uma aposta apostando Maçãs. Você pode apostar no máximo 50 maçãs por vez. Para manter as coisas legais, só posso aceitar maçãs.";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "Mas ei, se todo esse zeny está queimando um buraco no seu bolso, vá para Fruitz e você pode comprar quantas maçãs quiser, playah~";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "Agora, começamos comigo rolando dois dados de 6 faces. Quando for a sua vez, você jogará dois dados de 6 faces. Depois disso, nós dois teremos a opção de rolar um terceiro dado.";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "Agora aqui está o importante. Se o seu total for superior a 12, você será eliminado, o que significa que você perde. Caso contrário, a pessoa com o maior total é o vencedor. Entendi?";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "Agora, você será o primeiro a decidir se vai ou não rolar o terceiro dado. Então, dependendo do seu resultado, vou rolar meu terceiro dado... Ou talvez não.";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "Quando você ganhar, receberá o dobro de Maçãs que apostou. Mas se acontecer de nós empatarmos, você recebe as Maçãs que você apostou de volta para você. Justo, certo?";
		close;
	case 1:
		break;
	}
	mes "["+ .@npc_name$ +"]";
	mes "Ooh, então você vai brincar comigo? Excelente! Quantas maçãs você gostaria de apostar? Lembre-se, você pode apostar até 50 maçãs. Se você deseja cancelar, digite '0'.";
	next;
	while(1) {
		input .@amount;
		if (.@amount == 0) {
			mes "["+ .@npc_name$ +"]";
			mes "Mudou de ideia?";
			mes "Eu entendo. Bem, então, espero que possamos jogar algum dia.";
			close;
		}
		else if (.@amount < 1 || .@amount > 50) {
			mes "["+ .@npc_name$ +"]";
			mes "Você não pode apostar mais de 50 Maçãs. Lembre-se, precisamos manter essas apostas razoáveis. Insira um valor não superior a 50.";
			next;
			continue;
		}
		mes "["+ .@npc_name$ +"]";
		mes "Então você estará";
		mes "apostando^FF0000"+.@amount+"^000000 Maçã.";
		mes "É isso mesmo?";
		next;
		if (select("Sim:Não") == 2) {
			mes "["+ .@npc_name$ +"]";
			mes "Hum, cometeu um erro?";
			mes "Certo, por favor insira o número de maçãs que você deseja colocar nesta aposta";
			next;
			continue;
		}
		if (countitem(512) <.@amount) {
			mes "Sinto muito, mas parece que você não tem Maçãs suficientes para esta aposta... Você não pode jogar se não puder jogar, sabe.";
			next;
			continue;
		}
		delitem 512,.@amount;
		mes "["+ .@npc_name$ +"]";
		mes "Bom!";
		mes "Agora podemos começar este jogo! Eu vou rolar primeiro~";
		break;
	}
	mes "^3355FF*Rolando e roncando*^000000";
	next;
	set .@giveapple, .@amount*2;
	set .@table1, rand(1,6);
	set .@table2, rand(1,6);
	set .@tablesub, .@table1 + .@table2;
	set .@tabletotal, .@tablesub;
	mes "["+ .@npc_name$ +"]";
	mes "Eu tenho um ^0000FF" + .@table1 + "^000000 e um ^0000FF" + .@table2 + "^000000.";
	mes "Isso é um total de ^0000FF" + .@tablesub + "^000000.";
	mes "^FF0000" + strcharinfo(0) + "^000000, agora é sua vez.";
	next;
	select("Lançar Dados.");
	mes "^3355FF*Rolando e roncando*^000000";
	set .@player1, rand(1,6);
	set .@player2, rand(1,6);
	set .@playersub, .@player1 + .@player2;
	if (.@playersub > 9 && .@amount > 39) {
		set .@player1, rand(1,6);
		set .@player2, rand(1,6);
		set .@playersub, .@player1 + .@player2;
	}
	set .@playertotal, .@playersub;
	next;
	mes "["+ .@npc_name$ +"]";
	mes "^FF0000" + strcharinfo(0) + "^000000, you have ^FF0000" + .@player1 + "^000000 and ^FF0000" + .@player2 + "^000000. The total is ^FF0000" + .@playersub + "^000000 .";
	next;
	mes "["+ .@npc_name$ +"]";
	if(.@playersub == .@tablesub) {
		mes "Currently my total is ^0000FF" + .@tablesub + "^000000 and ^FF0000" + strcharinfo(0) + "^000000, your total is ^FF0000" + .@playersub + "^000000. We are making an even game. Would you like to cast dice again?";
	} else if (.@playersub > .@tablesub) {
		mes "Currently my total is ^0000FF" + .@tablesub + "^000000 and ^FF0000" + strcharinfo(0) + "^000000, your total is ^FF0000" + .@playersub + "^000000. ^FF0000" + strcharinfo(0) + "^000000, you are currently winning this game. Would you like to cast dice again?";
	} else if(.@tablesub > .@playersub) {
		mes "Currently my total is ^0000FF" + .@tablesub + "^000000 and ^FF0000" + strcharinfo(0) + "^000000, your total is ^FF0000" + .@playersub + "^000000. I am winning this game. Would you like to cast dice again?";
	}
	next;
	switch (select("Cast dice.:Cancel.")) {
	case 1:
		mes "^3355FF*Rolling and rumbling*^000000";
		set .@player3, rand(1,6);
		set .@playertotal, .@playertotal + .@player3;
		next;
		mes "["+ .@npc_name$ +"]";
		if (.@playertotal > 12) {
			mes "^FF0000" + strcharinfo(0) + "^000000, you got ^FF0000" + .@player3 + "^000000 and the total is now ^FF0000" + .@playertotal + "^000000. You lost this game. I am sorry but please try again.";
			close;
		}
		else if (.@playertotal < .@tablesub) {
			mes "^FF0000" + strcharinfo(0) + "^000000, you got ^FF0000" + .@player3 + "^000000 and the total is now ^FF0000" + .@playertotal + "^000000. Even though you casted dice again, still your total is smaller than mine. You lost the game. I am sorry and please try again.";
			close;
		}
		else if (.@playertotal == .@tablesub) {
			if (.@tablesub > 8) {
				mes "^FF0000" + strcharinfo(0) + "^000000, you got ^FF0000" + .@player3 + "^000000 and the total is now ^FF0000" + .@playertotal + "^000000. I don't want to take any risk, let's end this game in a draw. Let's play again some other time~";
				close2;
				getitem 512,.@amount; //Apple
				end;
			}
			mes "Tudo bem.";
			mes "Deixe-me jogar os dados novamente.";
		}
		else {
			mes "^FF0000" + strcharinfo(0) + "^000000, you got ^FF0000" + .@player3 + "^000000 and the total is now ^FF0000" + .@playertotal + "^000000. Now it is my turn.";
		}
		break;
	case 2:
		mes "["+ .@npc_name$ +"]";
		if (.@playersub > .@tablesub) {
			mes "Entendo, você não quer correr o risco de perder o jogo. Certo, deixe-me lançar dados novamente.";
		}
		else if (.@playersub == .@tablesub) {
			if (.@tablesub > 8) {
				mes "Entendo, você não quer correr o risco de perder este jogo. Nem eu, vamos terminar este jogo empatado. Vamos jogar de novo em outra hora~";
				close2;
				getitem 512,.@amount; //Maçã
				end;
			}
			mes "Tudo bem.";
			mes "Deixe-me jogar os dados novamente.";
		}
		else {
			mes "Não faria mal tentar.";
			mes "Bem, desta vez eu ganho.";
			mes "Desculpe, vamos tentar jogar novamente algum dia.";
			close;
		}
		break;
	}
	next;
	mes "^3355FF*Rolando e estrondoso*^000000";
	set .@table3, rand(1,6);
	set .@tabletotal, .@tabletotal + .@table3;
	next;
	mes "["+ .@npc_name$ +"]";
	if (.@tabletotal > 12) {
		mes "Eu obtive ^0000FF" + .@table3 + "^000000 e o total agora ^0000FF" + .@tabletotal + "^000000. Perdi este jogo porque meu total ultrapassou 12. Deixe-me dar-lhe minhas maçãs. Parabéns, foi um ótimo jogo.";
		close2;
		getitem 512,.@giveapple; //Apple
		end;
	}
	else if (.@playertotal > .@tabletotal) {
		mes "Eu obtive ^0000FF" + .@table3 + "^000000 e o total agora ^0000FF" + .@tabletotal + "^000000. Com total ^FF0000" + .@playertotal + "^000000 você ganhou este jogo, ^FF0000" + strcharinfo(0) + "^000000. Deixe-me dar-lhe as minhas maçãs. Foi um grande jogo e espero que voltemos a jogar em outras oportunidades.";
		close2;
		getitem 512,.@giveapple; //Apple
		end;
	}
	else if (.@playertotal == .@tabletotal) {
		mes "Eu obtive ^0000FF" + .@table3 + "^000000 e o total agora ^0000FF" + .@tabletotal + "^000000. Com total ^FF0000" + .@playertotal + "^000000 esse jogo sair mesmo, ^FF0000" + strcharinfo(0) + "^000000. Deixe-me devolver sua maçã. Foi um grande jogo e espero que voltemos a jogar em outras oportunidades.";
		close2;
		getitem 512,.@amount; //Apple
		end;
	}
	else if (.@playertotal < .@tabletotal) {
		mes "Eu obtive ^0000FF" + .@table3 + "^000000 e o total agora ^0000FF" + .@tabletotal + "^000000. Com total ^FF0000" + .@playertotal + "^000000 você perdeu este jogo, ^FF0000" + strcharinfo(0) + "^000000. Desculpe, mas tente novamente.";
		close;
	}
}
